buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.12.0'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'robolectric'

repositories {
        mavenCentral()
}

android {
    compileSdkVersion 'Google Inc.:Glass Development Kit Preview:19'
    buildToolsVersion '20.0.0'
    defaultConfig {
        applicationId 'com.augmate.sdk'
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName '1.0'

        ndk {
            // we now have ZXing shared libs to link for both x86 and arm-v7a :)
            String scannerDir = file('.').absolutePath
            ldLibs "log -lzxing" + " -L" + scannerDir + "/libs/lib/armeabi-v7a/" + " -L" + scannerDir + "/libs/lib/x86/"
            cFlags "-fexceptions -I" + scannerDir + "/includes/"
            moduleName "native-scanner"
            stl "gnustl_shared"
            abiFilters "x86", "armeabi-v7a"
        }
    }
    sourceSets {
        main {
            jni.srcDirs = ['src/main/jni']
            jniLibs.srcDirs = ['libs']
        }

        androidTest {
            setRoot('src/test')
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        buildTypes {
            debug {
                jniDebugBuild true
            }
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

task javah {
    ant.javah(
            class: 'com.augmate.sdk.scanner.NativeUtils',
            outputFile: 'src/main/jni/NativeUtils.h',
            classpath: 'src/main/java'
    )
}

preBuild.dependsOn javah

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'

    // FIXME: gradle 1.12 can't do this :(
//    afterTest { descriptor, result ->
//        println "Executing test for {$descriptor.name} with result: ${result.resultType}"
//    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // common 3rd party libs
    compile 'org.apache.commons:commons-math3:3.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'uk.com.robust-it:cloning:1.7.4'                // cloning
    compile 'org.objenesis:objenesis:2.1'                   // reflection
    compile 'com.google.code.gson:gson:2.3'                 // json
    compile 'joda-time:joda-time:2.3'                       // time

    // FIXME: get rid of this dependency
    compile 'com.android.support:support-v4:19.+'

    // dependency injection
    compile ('org.roboguice:roboguice:2.0') {
        exclude module: 'asm'
    }
    compile ('com.google.inject.extensions:guice-assistedinject:3.0'){
        exclude group: 'com.google.inject', module: 'guice'
    }

    // testing
    androidTestCompile 'com.google.android:android:4.1.1.4'
    androidTestCompile 'org.robolectric:robolectric:2.+'
    androidTestCompile 'org.assertj:assertj-core:1.6.1'
    androidTestCompile 'junit:junit:4.11'
}

apply plugin: 'idea'

idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}
